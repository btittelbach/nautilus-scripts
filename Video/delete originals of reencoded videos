#!/bin/zsh
setopt extendedglob cshnullglob braceccl histsubstpattern
type fuser || exit 1
type zenity || exit 1
zmodload -a  zsh/stat    zstat
(
for x in *.(x265|vp9|av1|x265opus|x265aac|hevc_nvenc).(mp4|webv|mkv); do
  y=( "${x:r:r}".(MOV|mov|MP4|mp4|MTS|mts|mkv)([1]N));
  if [[ -n $y && -s $x && -f $y ]]; then
    if fuser -s "$y"; then
        echo "Newly transcoded video is in use and may still be transcoding. Skipping $x"
        continue
    fi
    if fuser -s "$x"; then
        echo "Original is in use and may still be transcoding. Skipping $x"
        continue
    fi
    if [[ $(zstat -N +size "$x") -ge $(zstat -N +size "$y") ]]; then
      echo "Transcoded File is larger than original. No space saved. Skipping $x"
      continue
    fi
    rm -v "$y"
  fi
done
echo "=== Done ==="
) | zenity --text-info --text "Deleting Originals of transcoded videos" --title "Deleting Originals of transcoded videos" --auto-scroll --width 1024 --height 640 
